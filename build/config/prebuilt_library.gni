##----------------------------------------------------------------------------##
## This file is distributed under the MIT License.                            ##
## See LICENSE.txt for details.                                               ##
## Copyright (C) Tran Tuan Nghia <trantuannghia95@gmail.com> 2022             ##
##----------------------------------------------------------------------------##

template("prebuilt_library") {
  local_public_configs = []
  if (defined(invoker.static_libs)) {
    config_name = target_name + "_config"
    config(config_name) {
      libs = invoker.static_libs
      forward_variables_from(invoker, "*")
    }
    local_public_configs = [ ":$config_name" ]
  }
  local_deps = []
  if (defined(invoker.shared_libs)) {
    copy_dll_name = target_name + "_dll"
    copy(copy_dll_name) {
      sources = invoker.shared_libs

      outputs = [
        "$root_out_dir/{{source_file_part}}"
      ]
    }
    local_deps = [ ":$copy_dll_name" ]

  }

  # Dummy target
  source_set(target_name) {
    public_configs = local_public_configs
    deps = local_deps
  }
}
